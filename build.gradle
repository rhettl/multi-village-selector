plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.10-SNAPSHOT" apply false
    id "com.github.johnrengelman.shadow" version "8.1.1" apply false
    id "jacoco"
}

// Load local gradle properties if they exist (for dev overrides)
def localPropertiesFile = file('gradle.properties.local')
if (localPropertiesFile.exists()) {
    def localProperties = new Properties()
    localPropertiesFile.withInputStream { localProperties.load(it) }
    localProperties.each { key, value ->
        project.ext.set(key, value)
    }
}

architectury {
    minecraft = project.minecraft_version
}

subprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    // Platform modules set their own archivesName with platform suffix
    // Only set base name for common module
    if (project.name == "common") {
        base {
            archivesName = project.archives_base_name
        }
    }
    version = project.mod_version
    group = project.maven_group

    repositories {
        mavenCentral()
        maven { url "https://maven.architectury.dev/" }
        maven { url "https://maven.neoforged.net/releases/" }
    }

    dependencies {
        // JSON5 parser - used by all modules
        implementation "de.marhali:json5-java:3.0.0"
    }

    java {
        withSourcesJar()
        toolchain.languageVersion = JavaLanguageVersion.of(21)
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release = 21
    }
}

allprojects {
    apply plugin: "java"

    java {
        toolchain.languageVersion = JavaLanguageVersion.of(21)
    }
}
