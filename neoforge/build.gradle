plugins {
    id "dev.architectury.loom"
    id "com.github.johnrengelman.shadow"
}

base {
    archivesName = "${rootProject.archives_base_name}-neoforge"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    silentMojangMappingsLicense()
}

// Configuration for including common module and shaded dependencies
configurations {
    common
    shadowCommon
    shade  // For bundling external dependencies
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
    implementation.extendsFrom shade
}

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
    mappings loom.officialMojangMappings()

    // NeoForge
    neoForge "net.neoforged:neoforge:${rootProject.neoforge_version}"

    // Architectury API (NeoForge)
    modApi "dev.architectury:architectury-neoforge:${rootProject.architectury_version}"

    // Optional config GUI
    modCompileOnly("dev.isxander:yet-another-config-lib:${rootProject.yacl_neoforge_version}") {
        transitive = false
    }

    // Common module
    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive false }

    // JSON5 parser (shaded into jar)
    shade "de.marhali:json5-java:3.0.0"

    // Testing
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.10.0"
    testImplementation project(":common")
}

test {
    useJUnitPlatform()
}

processResources {
    inputs.property "mod_version", project.mod_version

    filesMatching("META-INF/neoforge.mods.toml") {
        expand "mod_version": project.mod_version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"
    // Exclude common mixin config - we use NeoForge-specific one without refmap
    exclude "multi_village_selector.mixins.json"
    exclude "multivillageselector-common-refmap.json"
    configurations = [project.configurations.shadowCommon, project.configurations.shade]
    archiveClassifier = "dev-shadow"

    // Relocate JSON5 to avoid conflicts with other mods
    relocate "de.marhali.json5", "com.rhett.multivillageselector.shadow.json5"
}

remapJar {
    injectAccessWidener = true
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
